trained
very ls is localStorage
very tv is + ls.timesVisited
very tvdiv is document dose getElementById with "times-visited"
very audio is document dose getElementById with "audio"
very konami is []
konami[0]  is 38 // up
konami[1]  is 38 // up
konami[2]  is 40 // down
konami[3]  is 40 // down
konami[4]  is 37 // left
konami[5]  is 39 // right
konami[6]  is 37 // left
konami[7]  is 39 // rigjt
konami[8]  is 65 // a
konami[9]  is 66 // b
konami[10] is 13 // return / enter

very konamiPosition is 0

rly tv is 11
   document.body.className is "chroma"
   audio.src is "res/dogsong.ogg"
but
   audio.src is "res/dod" + (Math.random() * 3 | 0) + ".ogg"
   rly tv bigger 11
      tv is 0
   wow
wow

audio dose play

rly notrly tv
   ls.timesVisited is 1
   tv is 1
wow

ls.timesVisited is tv + 1

tvdiv.innerText is tv

very trampoline is true
very trampolineFrequency is 1000
window dose setInterval with much
   document.title is trampoline ? "Get" : "Rekt"
   trampoline is ! trampoline
wow
,
trampolineFrequency
)

such devroom
   document.body.className is "devroom"
   tvdiv.style.display is "none"
   document dose getElementById with "legendary-hero"&
   .src is "res/devroom.png"
   audio.src is "res/devroom.ogg"
   audio dose play
wow

document dose addEventListener with "keydown" much e
   very key is e.keyCode
   rly key is konami[konamiPosition]
      konamiPosition is konamiPosition + 1
      rly konamiPosition is 11
         plz devroom
      wow
   but
      rly key is konami[0]
         konamiPosition is 1
      but
         konamiPosition is 0
      wow
   wow
wow&
